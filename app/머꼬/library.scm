(정의 (머리 x) (머 x))

(정의 (꼬리 x) (꼬 x))

(정의 (머머 x) (머 (머 x))) 
(정의 (꼬머 x) (머 (꼬 x)))

(정의 (그대로 x) 
  x)
(정의 (아톰? x) 
  (그리고 (부정 (짝? x)) 
          (부정 (공? x))))
(정의 (리스트? x)
  (또는 (짝? x)
        (공? x)))

(정의 (절댓값 x) 
  (만약 (< x 0) 
    (- 0 x) 
    x))

(정의 (머리돌기 proc init list) 
  (만약 list 
    (머리돌기 proc (proc init (머 list)) (꼬 list)) 
    init))

(정의 (꼬리돌기 proc init list) 
  (만약 list 
    (proc (머 list) (꼬리돌기 proc init (꼬 list))) 
    init))


(정의 (리스트 . items) 
  (꼬리돌기 짝 공 items))

(정의 (거꾸로 list) 
  (머리돌기 (람다 (a x) (짝 x a)) 공 list))

(정의 (한맵 proc list) 
  (꼬리돌기 (람다 (x rest) (짝 (proc x) rest)) 공 list))

(정의 (맵 proc . arg-lists) 
  (만약 (머 arg-lists) 
    (짝 (적용 proc (한맵 머 arg-lists)) 
    (적용 맵 (짝 proc (한맵 꼬 arg-lists)))) 공))

(정의 (접합 a b) 
  (꼬리돌기 짝 b a))

(매크로 (특이인용 x) 
  (만약 (짝? x) 
    (만약 (같다? (머 x) '비인용) 
      (꼬머 x) 
      (만약 (짝? (머 x)) 
        (만약 (같다? (머머 x) '비인용연결) 
          (리스트 '접합 (꼬머 (머 x)) 
          (리스트 '특이인용 (꼬 x))) 
        (리스트 '짝 (리스트 '특이인용 (머 x)) (리스트 '특이인용 (꼬 x)))) 
    (리스트 '짝 (리스트 '특이인용 (머 x)) (리스트 '특이인용 (꼬 x))))) 
  (리스트 '인용 x)))

